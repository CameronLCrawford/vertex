data block {
        size: [16].
        next: <[block]>.
}

global free_list: <[block]>.

' size is the number of bytes requested by the caller
routine malloc(size: [16]) -> <[0]>;
        current_block: <[block]>.
        prev_block: <[block]>.
        allocated: <[0]>.
        split: <[block]>.
        free_size: [16].
{
        current_block = free_list.
        prev_block = <[block]> 0:16.
        if $free_list / size < size + #[block] {
                return allocated.
        }
        loop {
                if $current_block / size >= size {
                        allocated = <[0]> ([16]current_block + #[block]).
                        if [16]prev_block { ' ie. not at front of freelist
                                if $current_block / size = size {
                                        $prev_block / next = $current_block / next.
                                        $current_block / size = size.
                                        $prev_block / size = $prev_block / size - size.
                                        break.
                                }
                                split = <[block]> ([16]current_block + size + #[block]).
                                $split / size = $current_block / size - size - #[block].
                                $split / next = $current_block / next.
                                $prev_block / next = split.
                                $current_block / size = size.
                                $prev_block / size = $prev_block / size - size.
                                break.
                        }
                        free_size = $free_list / size.
                        $free_list / size = size.
                        free_list = <[block]> ([16]free_list + size + #[block]).
                        $free_list / size = free_size - size - #[block].
                        $free_list / next = $current_block / next.
                        break.
                }
                prev_block = current_block.
                current_block = $current_block / next.
        }
        return allocated.
}

routine entry () -> [0]
{
        free_list = <[block]> 256:16.
        $free_list / next = <[block]> 0:16.
        $free_list / size = 100:16.
        !malloc(20:16).
        !malloc(20:16).
        return.
}
